Option Explicit

Sub Comparison()
 
    Dim i As Long
    Dim k As Long
    Dim x As Integer
    Dim maxrow As Long
    Dim maxroww As Long
    Dim src As String
    Dim dest As String
    Dim filePath As String

    MsgBox "比較元（新しいデータ）を開いてください"
    Call opfn
    src = ActiveWorkbook.Name '比較元の名前

    MsgBox "比較先（古いデータ）を開いてください"
    Call opfn
    dest = ActiveWorkbook.Name '比較先の名前
    
    maxrow = Workbooks(src).Worksheets(1).Cells(Rows.Count, 1).End(xlUp).Row
    maxroww = Workbooks(dest).Worksheets(1).Cells(Rows.Count, 1).End(xlUp).Row
    
    i = 2 '2行目から見る
    k = 3 '3列目から見る
    
    Do Until i > maxrow
    
        If i = 3000 Then
            MsgBox "3000件を超えたので停止しました"
            Exit Do
        End If
    
        Dim Rng As Range
            With Workbooks(dest).Worksheets(1)
                Set Rng = .Range(.Cells(2, 1), .Cells(maxroww, 1)).Find(What:=Workbooks(src).Worksheets(1).Cells(i, 1), LookIn:=xlValues, LookAt:=xlWhole)
            End With
            With Workbooks(src).Worksheets(1)
                If Not Rng Is Nothing Then
                    Do Until k > 35
                        x = StrComp(.Cells(i, k), Workbooks(dest).Worksheets(1).Cells(Rng.Row, k))
                        If x <> 0 Then
                            .Cells(i, k).Interior.ColorIndex = 40
                        End If
                        k = k + 1
                    Loop
                    k = 3
                Else
                    .Cells(i, 1).Interior.ColorIndex = 40
                    .Cells(i, 2).Interior.ColorIndex = 40
                End If
            End With
        
        i = i + 1
      
    Loop
    
    MsgBox "比較に成功しました！"
    Workbooks(dest).Close
    Workbooks(src).Activate
    Columns("A:AI").AutoFit '列幅を自動調整
    filePath = Application.GetSaveAsFilename ' ダイアログを表示
    If filePath = "False" Then
        Exit Sub ' キャンセル
    End If
    Call Workbooks(src).SaveAs(filePath) ' 名前を付けて保存
    
End Sub

Function opfn() As String
    Dim osname As String
    Dim OpenFileName As String
    osname = Application.OperatingSystem
    If osname Like "Windows*" Then
        'Windows用
        OpenFileName = Application.GetOpenFilename("Microsoft Excelブック,*.xls?")
    Else
        'Mac用
        OpenFileName = Application.GetOpenFilename()
    End If
    If OpenFileName <> "False" Then
        Workbooks.Open OpenFileName
    Else
        MsgBox "キャンセルされました"
        End '終了する
    End If
End Function
