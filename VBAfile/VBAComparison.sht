Option Explicit

Sub Comparison()
 
    Dim i As Long
    Dim k As Long
    Dim x As Integer
    Dim srcmaxrow As Long
    Dim destmaxrow As Long
    Dim srcmaxcol As Long
    Dim destmaxcol As Long
    Dim src As String
    Dim dest As String
    Dim filePath As String
    Dim osname As String
    osname = Application.OperatingSystem

    MsgBox "比較元（新しいデータ）を開いてください"
    Call opfn(osname)
    src = ActiveWorkbook.Name '比較元の名前

    MsgBox "比較先（古いデータ）を開いてください"
    Call opfn(osname)
    dest = ActiveWorkbook.Name '比較先の名前
    
    srcmaxrow = Workbooks(src).Worksheets(1).Cells(Rows.Count, 1).End(xlUp).Row
    destmaxrow = Workbooks(dest).Worksheets(1).Cells(Rows.Count, 1).End(xlUp).Row
    srcmaxcol = Workbooks(src).Worksheets(1).Cells(1, Columns.Count).End(xlToLeft).Column
    destmaxcol = Workbooks(dest).Worksheets(1).Cells(1, Columns.Count).End(xlToLeft).Column

    If srcmaxcol <> destmaxcol Then
        MsgBox "データの数が違います"
        End
    End If

    i = 2 '2行目から見る
    k = 3 '3列目から見る
    
    Do Until i > srcmaxrow
    
        If i = 3000 Then
            MsgBox "3000件を超えたので停止しました"
            Exit Do
        End If
    
        Dim Rng As Range
            With Workbooks(dest).Worksheets(1)
                Set Rng = .Range(.Cells(2, 1), .Cells(destmaxrow, 1)).Find(What:=Workbooks(src).Worksheets(1).Cells(i, 1), LookIn:=xlValues, LookAt:=xlWhole)
            End With
            With Workbooks(src).Worksheets(1)
                If Not Rng Is Nothing Then
                    Do Until k > srcmaxcol
                        If IsError(Workbooks(dest).Worksheets(1).Cells(Rng.Row, k).Value) Then
                            Workbooks(dest).Worksheets(1).Cells(Rng.Row, k).Value = ""
                        End If
                        x = StrComp(.Cells(i, k), Workbooks(dest).Worksheets(1).Cells(Rng.Row, k))
                        If x <> 0 Then
                            .Cells(i, k).Interior.ColorIndex = 40
                        End If
                        k = k + 1
                    Loop
                    k = 3
                Else
                    .Cells(i, 1).Interior.ColorIndex = 40
                    .Cells(i, 2).Interior.ColorIndex = 40
                End If
            End With
        
        i = i + 1
      
    Loop
    
    MsgBox "比較に成功しました！"
    Workbooks(dest).Close SaveChanges:=False
    Workbooks(src).Activate
    Range(Columns(1), Columns(srcmaxcol)).AutoFit '列幅を自動調整
    With Application
        .WindowState = xlNormal   '標準
        If osname Like "Windows*" Then 'Macだとウィンドウサイズを設定できない？
            .Width = 1200
            .Height = 800
        End If
    End With
    Cells(1, 1).Select
    If osname Like "Windows*" Then
        'Windows用
        filePath = Application.GetSaveAsFilename(InitialFileName:="差分.xlsx", FileFilter:="Excelファイル,*.xlsx") ' ダイアログを表示
    Else
        'Mac用
        filePath = Application.GetSaveAsFilename() ' ダイアログを表示
    End If
    If filePath = "False" Then
        Exit Sub ' キャンセル
    End If
    Workbooks(src).SaveAs filePath ' 名前を付けて保存
    
End Sub

Function opfn(osname) As String
    Dim OpenFileName As String
    If osname Like "Windows*" Then
        'Windows用
        OpenFileName = Application.GetOpenFilename("Microsoft Excelブック,*.xls?")
    Else
        'Mac用
        OpenFileName = Application.GetOpenFilename()
    End If
    If OpenFileName <> "False" Then
        Workbooks.Open OpenFileName
    Else
        MsgBox "キャンセルされました"
        End '終了する
    End If
End Function
